buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'checkstyle'

group = 'com.microsoft.rest'
version = '1.0.0-SNAPSHOT'

checkstyle {
    toolVersion = "6.18"
    configFile = new File("$rootDir/ClientRuntimes/Java/build-tools/src/main/resources/checkstyle.xml")
    configProperties = [samedir: "$rootDir/ClientRuntimes/Java/build-tools/src/main/resources"]
    reportsDir = new File("$rootDir/ClientRuntimes/Java/build-tools/reports")
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okio:okio:1.7.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.1.1'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0-beta4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.7.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
    testCompile 'junit:junit-dep:4.11'
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.10"
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(url: "ftp://waws-prod-bay-005.ftp.azurewebsites.windows.net/site/wwwroot/") {
                authentication(userName: username, password: password)
            }
            repository(url: "file://$buildDir/repository")
            pom.project {
                name 'Microsoft AutoRest Runtime for Java'
                description 'This is the client runtime for AutoRest generated Java clients.'
                url 'https://github.com/Azure/autorest'

                scm {
                    url 'scm:git:https://github.com/Azure/AutoRest'
                    connection 'scm:git:git://github.com/Azure/AutoRest.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'microsoft'
                        name 'Microsoft'
                    }
                }
            }
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: [javadoc]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    reports.getHtml()
    reports.html.destination = file("${projectDir}/../../../TestResults/JavaRuntime")
}

tasks.compileJava.dependsOn 'clean'

println sourceSets.main.runtimeClasspath.asPath
