buildscript {
    android {
        lintOptions {
            abortOnError false
        }
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven'

version = '1.0.0-SNAPSHOT'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0-SNAPSHOT"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    deployerJars
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.microsoft.aad:adal:1.1.11'
    compile 'com.microsoft.rest:client-runtime:1.0.0-SNAPSHOT'
    testCompile 'junit:junit:4.12'
    testCompile 'junit:junit-dep:4.11'
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.10"
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(url: "ftp://waws-prod-bay-005.ftp.azurewebsites.windows.net/site/wwwroot/") {
                authentication(userName: username, password: password)
            }
            repository(url: "file://$buildDir/repository")
            pom.setArtifactId "azure-android-client-authentication"
            pom.project {
                name 'Microsoft Azure AutoRest Authentication Library for Java'
                description 'This is the authentication library for AutoRest generated Azure Java clients.'
                url 'https://github.com/Azure/autorest'

                scm {
                    url 'scm:git:https://github.com/Azure/AutoRest'
                    connection 'scm:git:git://github.com/Azure/AutoRest.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'microsoft'
                        name 'Microsoft'
                    }
                }
            }
        }
    }
}

task versionInfo(type:Exec){
    commandLine 'git rev-parse HEAD'.split()
    ext.versionfile = new File("${projectDir}/.gitrevision")
    standardOutput = new ByteArrayOutputStream()

    doLast {
        versionfile.text = "Azure/autorest#" + standardOutput.toString()
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: [javadoc, versionInfo]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives file: file("${projectDir}/.gitrevision")
}
